plugins {
	id "dev.architectury.loom" version "1.0-SNAPSHOT"
	id "maven-publish"
}

import net.fabricmc.loom.task.RemapJarTask
import net.fabricmc.loom.task.RemapSourcesJarTask

def ENV = System.getenv()
sourceCompatibility = targetCompatibility = 1.8

archivesBaseName = "Manningham Mills"
version = "2.4.1"

repositories {
	maven {
		url = "https://jitpack.io"
		content {
			includeGroup("com.github.Chocohead")
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:1.14.4"
	mappings "net.fabricmc:yarn:1.14.4+build.18:v2"
	modCompileOnly "net.fabricmc:fabric-loader:0.7.0+build.171"
	modLocalRuntime "net.fabricmc:fabric-loader:0.7.0+build.171"
	modLocalRuntime "com.github.Chocohead:Fabric-ASM:v2.3"
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
	options.release = 8
}

tasks.withType(GenerateModuleMetadata) {
	enabled = false
}

processResources {
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
	inputs.property "version", project.version
}

sourceSets {
	main {
		java {
			srcDir "src"
		}
		resources {
			srcDir "resources"
		}
	}
	example {
		compileClasspath += main.compileClasspath + main.output
		annotationProcessorPath = main.annotationProcessorPath
		java {
			srcDir "example/src"
		}
		resources {
			srcDir 'example/resources'
			exclude "access-transformations.txt"
		}
	}
}

java {
	withSourcesJar()
}

task exampleJar(type: Jar, dependsOn: exampleClasses) {
	classifier "example-raw"
	from sourceSets.example.output
}

task exampleSourcesJar(type: Jar, dependsOn: exampleClasses) {
	classifier "example-sources-raw"
	from sourceSets.example.allSource
}

task exampleRemappedJar(type: RemapJarTask, dependsOn: exampleJar) {
	classifier "example"
	inputFile.set tasks.exampleJar.archiveFile
	
}

task exampleRemappedSourcesJar(type: RemapSourcesJarTask, dependsOn: exampleSourcesJar) {
	classifier "example-sources"
	inputFile.set tasks.exampleSourcesJar.archiveFile
}

build.dependsOn exampleRemappedJar, exampleRemappedSourcesJar

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = "me.shedaniel"
			artifactId = "mm"
			from components.java
		}
	}

	repositories {
		if (ENV.MAVEN_PASS != null) {
			maven {
				url = "https://deploy.shedaniel.me/"
				credentials {
					username = "shedaniel"
					password = ENV.MAVEN_PASS
				}
			}
		}
	}
}
